apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "de.mannodermaus.android-junit5"
apply from: '../signing.gradle'

android {
    compileSdkVersion androidCompileSdkVersion
    defaultConfig {
        applicationId "xyz.pavelkorolev.githubrepos"
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
        versionCode appVersionCode
        versionName appVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArgument "runnerBuilder", "de.mannodermaus.junit5.AndroidJUnit5Builder"
    }
    signingConfigs {
        release {
            def signingConfig = loadSigningConfig()
            storeFile signingConfig.keystoreFile
            storePassword signingConfig.password
            keyAlias signingConfig.alias
            keyPassword signingConfig.aliasPassword
        }
    }
    buildTypes {
        debug {
            versionNameSuffix "-debug"
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
        }
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    testOptions {
        junitPlatform {
            filters {
                engines {
                    include 'spek2'
                }
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE.md'
        exclude 'META-INF/LICENSE-notice.md'
        exclude 'META-INF/spek-dsl.kotlin_module'
        exclude 'META-INF/spek-runtime.kotlin_module'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }

}

dependencies {
    implementation project(":helper")
    implementation project(':network')

    implementation libraries.kotlin
    implementation libraries.appCompat
    implementation libraries.androidxCoreKtx
    implementation libraries.androidxLifecycle

    implementation libraries.material

    implementation libraries.timber
    implementation libraries.koin
    implementation libraries.koinAndroidViewModel
    implementation libraries.koinAndroidScope

    implementation libraries.rxJava
    implementation libraries.rxAndroid
    implementation libraries.rxBinding
    implementation libraries.rxRelay

    implementation libraries.epoxy

    implementation libraries.glide
    kapt libraries.glideCompiler

    testImplementation testLibraries.spek
    testImplementation testLibraries.spekRunnerJunit
    testImplementation testLibraries.kotlinReflect
    testImplementation testLibraries.kotlinAssert
    testImplementation testLibraries.mockitoKotlin

    androidTestImplementation testLibraries.spek
    androidTestImplementation(testLibraries.spekRunnerJunit) {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-reflect'
    }
    androidTestImplementation testLibraries.androidXRunner
    androidTestImplementation testLibraries.androidXRules
    androidTestImplementation testLibraries.espresso
    androidTestImplementation testLibraries.kotlinAssert

    androidTestImplementation testLibraries.androidJUnit5
    androidTestImplementation(testLibraries.androidJUnit5Runner) {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-reflect'
    }
}
